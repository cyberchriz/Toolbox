// Vulkan/GLSL compute shader
// author: Christian Suer (github: 'cyberchriz')

#version 450
#define MAX_DIMENSIONS 10

// setup specialization constants
// in "main" use gl_WorkGroupSize.x / gl_WorkGroupSize.y / gl_WorkGroupSize.z to get the actual workgroup size
layout(local_size_x_id = 0, local_size_y_id = 1, local_size_z_id = 2) in;

// setup buffers
layout(set = 0, binding = 0) buffer data_buffer {float data[];};
layout(set = 0, binding = 1) buffer shape_buffer {uint shape[];};

// setup push constants layout
layout(push_constant) uniform push_constants {
    uint N;
    uint dimensions;
};


// main function
void main() {
    if (gl_GlobalInvocationID.x < N) {

        // get the multidimensional index of the data buffer
        uint array_index[MAX_DIMENSIONS];
        uint flat_index = gl_GlobalInvocationID.x;
        for (int i = int(dimensions) - 1; i >= 0; --i) {
            array_index[i] = flat_index % shape[i];
            flat_index = flat_index / shape[i];
        }

        // check if indices of the other dimensions are equal
        uint row_index = array_index[0];
        for (uint i = 1; i < dimensions; i++) {
            if (array_index[i] != row_index) {
                data[gl_GlobalInvocationID.x] = 0.0;
                return;
            }
        }
        data[gl_GlobalInvocationID.x] = 1.0;
    }
}
