// this Vulkan/GLSL compute shader implements dynamic resizing of the array;
// all elements with a corresponding element in the original array get copied;
// any new elements are initialized to the specified value as given via push constant

#version 450
#define WORKGROUP_SIZE 256

// setup buffers
layout(set = 0, binding = 0) buffer DataBuffer {
    float data[];
};
layout(set = 0, binding = 1) buffer ResultBuffer {
    float result[];
};

// setup push constants layout
layout(push_constant) uniform PushConstants {
    uint data_rows;
    uint data_cols;
    uint data_depth;

    uint result_rows;
    uint result_cols;
    uint result_depth;

    float init_value;
};

// setup local size according to workgroup size
layout(local_size_x = WORKGROUP_SIZE) in;

// setup global variables
uint data_elements = data_rows * data_cols * data_depth;
uint result_elements = result_rows * result_cols * result_depth;
uint data_subspace_x = data_cols * data_depth;
uint result_subspace_x = result_cols * result_depth;


// main function
void main() {
    uint result_flat_index = gl_GlobalInvocationID.x;
    if (result_flat_index < result_elements) {
        uint result_3dx = uint(result_flat_index/result_subspace_x);
        uint result_3dy = uint((result_flat_index % result_subspace_x) / result_depth);
        uint result_3dz = uint((result_flat_index % result_subspace_x) % result_depth);
        uint data_flat_index = result_3dx * data_subspace_x + result_3dy * data_depth + result_3dz;
        if (result_3dx < data_rows && result_3dy < data_cols && result_3dz < data_depth) {
            result[result_flat_index] = data[data_flat_index];
        }
        else {
            result[result_flat_index] = init_value;
        }
    }
}